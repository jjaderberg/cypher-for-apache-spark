apply plugin: 'com.github.johnrengelman.shadow'

description = 'Okapi Shade contains the full Okapi stack and its relocated dependencies'

dependencies {
    compile project(':okapi-relational')
}

ext.excludedDependencyGroups = [
    'org.scala-lang',
    'org.scala-lang.modules',
]

pub.each { k, v ->
    v.artifacts = []
}

publishing {
    publications {
        shade(MavenPublication) { pub ->
            project.shadow.component(pub)
            artifact sourceJar
            artifact docJar
            pom pomConfig
            pom.withXml {
                def dependenciesNode = asNode().dependencies[0]
                project.configurations.compile.allDependencies.findAll {
                    excludedDependencyGroups.contains(it.group)
                }.each { dep ->
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', dep.group)
                    dependencyNode.appendNode('artifactId', dep.name)
                    dependencyNode.appendNode('version', dep.version)
                    dependencyNode.appendNode('scope', 'compile')
                }
            }
        }
    }
}

shadowJar {
    classifier = ''
    from generatePomFileForShadePublication {
        rename('pom-default.xml',"META-INF/maven/${project.group}/${project.name}/pom.xml")
    }
    def relocateTo = 'org.opencypher.relocated'
    def relocateFrom = [
            'cats',
            'fastparse',
            'machinist',
            'org.apache',
            'org.atnos',
            'org.parboiled',
            'sourcecode',
            'ujson',
            'upack',
            'upickle',
    ]
    relocateFrom.each { pkg ->
        relocate(pkg, "${relocateTo}.$pkg")
    }

    dependencies {
        excludedDependencyGroups.each {
            exclude(dependency("$it:"))
        }
    }
    exclude "META-INF/versions/**/*"
    exclude "META-INF/services/**/*"
    exclude "META-INF/NOTICE*"
    exclude "META-INF/LICENSE*"
    exclude "utf8.json"

    doLast {
        def scalaShadeExecutable = 'bin/scalashade-0.3.jar'
        def repairWorkingDir = "$buildDir/tmp/scalashade"
        def brokenJarPath = shadowJar.archivePath
        def repairedJarName
        def repairedJarPath
        mkdir(repairWorkingDir)
        relocateFrom.each { pkg ->
            println "repairing signature: from [$pkg] to [${relocateTo}.$pkg]"
            repairedJarName = "$pkg-${shadowJar.archiveName}"
            repairedJarPath = "$repairWorkingDir/$repairedJarName"
            println "reading from [$brokenJarPath], writing to [$repairedJarPath]"
            exec {
                executable 'java'
                args([
                    '-jar',
                    scalaShadeExecutable,
                    brokenJarPath,
                    repairedJarPath,
                    pkg,
                    "${relocateTo}.$pkg",
                ])
            }
            brokenJarPath = repairedJarPath
        }
        copy {
            into shadowJar.destinationDir
            from (repairWorkingDir) {
                include repairedJarName
            }
        }
    }
}

assemble.dependsOn shadowJar
